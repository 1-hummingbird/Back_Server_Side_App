plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.hummingbird'
version = '0.0.2-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-batch' // 배치
    compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'

	//Querydsl start
    def querydslVersion = "6.10.1"

    // Querydsl configuration
    implementation "io.github.openfeign.querydsl:querydsl-jpa:${querydslVersion}"
    implementation "io.github.openfeign.querydsl:querydsl-core:${querydslVersion}"
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:${querydslVersion}:jpa"
    
    // Required annotation processors
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
	//Querydsl end
	
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.10.0'// 쿼리 파라미터 로그
	// model mapper
	implementation 'org.modelmapper:modelmapper:3.2.2'

	//	jwt
	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.12.6'
	implementation group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.12.6'
	implementation group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.12.6'

	// smtp sender
	implementation 'com.sun.mail:jakarta.mail:2.0.1'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	
	// swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'

	// request by Dependabot
	implementation 'com.google.protobuf:protobuf-java:4.29.3'
	implementation 'io.netty:netty-common:4.1.117.Final'
	implementation 'org.springframework:spring-context:6.2.2'
	implementation 'org.springframework:spring-web:6.2.2'
	implementation 'org.springframework.security:spring-security-core:6.4.2'
	implementation 'org.apache.commons:commons-compress:1.27.1'
	implementation 'io.lettuce:lettuce-core:6.5.2.RELEASE'
	implementation 'ch.qos.logback:logback-core:1.5.16'

	//test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	// 테스트 코드에도 lombok 적용
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor'org.projectlombok:lombok'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Configure Querydsl source directory
def querydslDir = "src/main/generated"

// Clean the Querydsl generated sources directory
clean {
    delete file(querydslDir)
}

// Add Querydsl generated sources to Java source sets
sourceSets {
    main.java.srcDirs += [ querydslDir ]
}

// Configure the annotation processor output directory
tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}
